real money 500
int num1   0
int num2   0
int num3   0
int guess  0
int right  0
real bet   0
char re    n


print "		      Welcome to casino!" `
print "     Three random numbers from 1 to 5 will be generated" `
print "      You will need to make a bet and guess a number" `
print "If your number not any of those three casino takes your bet" `
print "If your number is encountered once  you take back your bet + win 150%" `
print "If your number is encountered twice you take back your bet + win 300%" `
print "If your number is encountered trice you take back your bet + win 900%" `


!start
print "You have: " $money $ ` `

!bet
print "Your bet: "
scan bet

doif $bet > $money goto nomoney
goto check_little

!nomoney
print "Not enught money!" ` `
goto bet

!check_little
doif $bet < 1 goto tolittle
goto guess

!tolittle
print "You shoud bet more than 0$!" ` `
goto bet


!guess
print "Your guess: "
scan guess

doif $guess > 5 goto bigguess
goto check_smallg

!bigguess
print "Number cannot be more than 5!" ` `
goto guess

!check_smallg
doif $guess < 1 goto smallguess
goto gennums

!smallguess
print "Number cannot be less than 1!" ` `
goto guess



!gennums
print `
wait 2
rand 1 5 num1
print $num1 "   "
wait 2
rand 1 5 num2
print $num2 "   "
wait 2
rand 1 5 num3
print $num3 "   " ` `


swap right 0
doif $guess = $num1 sum right 1
doif $guess = $num2 sum right 1
doif $guess = $num3 sum right 1
print "You got " $right " right!" `


doif $right = 0 goto lose
doif $right = 1 prod bet 1.5
doif $right = 2 prod bet 3.0
doif $right = 3 prod bet 9.0

print "You win " $bet $! ` `
goto sumwin

!lose
print "You lose " $bet $! ` `
prod bet -1.0

!sumwin
sum money $bet


print "Continue playing? [y/n]: "
scan re
doif $re = y goto restart
doif $re = Y goto restart
goto end
!restart
clear
goto start

!end
